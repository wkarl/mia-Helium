apply plugin: 'findbugs'

// Run findBugs + lint with gradle task check
check.dependsOn 'findBugs', 'lint'

// Treat all warnings as errors
tasks.withType(JavaCompile).all {
    options.compilerArgs << "-Xlint:all"
    options.compilerArgs << "-Werror"
}

// Find bugs task
task findBugs(type: FindBugs) {
    // Description
    description = 'Find bugs'
    group       = 'verification'

    // Aborting
    ignoreFailures = false
    effort         = 'max'

    // Use module sources + classes directories
    classpath = files()
    source    = fileTree('src/main/java/')
    classes   = fileTree('build/intermediates/classes/')

    // Exclude Android stuff
    excludeFilter = new File("${project.rootDir}/quality/rules/findbugs-rules.xml")

    // Report HTML
    reports {
        xml.enabled = false
        html.enabled = true
        html { destination "${project.rootDir}/build/reports/findbugs/findbugs-report.html"}
    }
}

// Lint settings
android {
    lintOptions {
        // Aborting
        abortOnError true

        // Ignore custom lint warnings
        lintConfig file("$project.rootDir/quality/rules/lint-rules.xml")

        // Report HTML
        xmlReport  false
        htmlReport true
        htmlOutput file("${project.rootDir}/build/reports/lint/lint-report.html")
    }
}
